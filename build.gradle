plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.tranngockhoa'
version '0.0.2'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}


dependencies {
    implementation 'org.seleniumhq.selenium:selenium-api:4.7.2'
    implementation 'org.seleniumhq.selenium:selenium-chrome-driver:4.7.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testRuntimeOnly 'org.seleniumhq.selenium:selenium-java:4.7.2'
    testImplementation 'commons-io:commons-io:2.11.0'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.property("osshUserName")
                password = project.property("osshPassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.tranngockhoa'
            version '0.0.2'
            artifactId = 'driver'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Awesome Driver'
                description = 'Custom Chrome Driver'
                url = 'https://github.com/TranNgocKhoa/awesome-driver.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tranngockhoa'
                        name = 'Tran Ngoc Khoa'
                        email = 'tranngoc.khoa.spk@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TranNgocKhoa/awesome-driver.git'
                    developerConnection = 'scm:git:ssh://github.com/TranNgocKhoa/awesome-driver.git'
                    url = 'https://github.com/TranNgocKhoa/awesome-driver.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}